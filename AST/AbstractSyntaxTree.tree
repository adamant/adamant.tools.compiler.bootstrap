◊namespace Adamant.Tools.Compiler.Bootstrap.AST;
◊base 'IAbstractSyntax';
◊prefix I;
◊list FixedList;
◊using System.Numerics;
◊using Adamant.Tools.Compiler.Bootstrap.Core;
◊using Adamant.Tools.Compiler.Bootstrap.Core.Operators;
◊using Adamant.Tools.Compiler.Bootstrap.Core.Promises;
◊using Adamant.Tools.Compiler.Bootstrap.Framework;
◊using Adamant.Tools.Compiler.Bootstrap.Symbols;
◊using Adamant.Tools.Compiler.Bootstrap.Types;

'IAbstractSyntax' = Span:'TextSpan';

// ---------- Special Parts
BodyOrBlock = Statements:Statement*;
ElseClause;
BlockOrResult: ElseClause;

// ---------- Bindings
Binding = Symbol:'BindingSymbol';
LocalBinding: Binding = Symbol:'NamedBindingSymbol';

// ---------- Declarations
// Note: in the AST all declarations are entities because there are no namespace declarations
Declaration = File:'CodeFile' Symbol:'Symbol';
/// A declaration that could contain executable code (i.e. invocable declaration or field declaration)
ExecutableDeclaration: Declaration;
InvocableDeclaration: Declaration = Symbol:'InvocableSymbol' Parameters:ConstructorParameter*;
ConcreteInvocableDeclaration: InvocableDeclaration, ExecutableDeclaration = Body;

// ---------- Non-Member Declarations
NonMemberDeclaration: Declaration;
ClassDeclaration: NonMemberDeclaration = Symbol:'ObjectTypeSymbol' Members:MemberDeclaration*;
FunctionDeclaration: NonMemberDeclaration, ConcreteInvocableDeclaration = Symbol:'FunctionSymbol' Parameters:NamedParameter* Body;

// ---------- Member Declarations
MemberDeclaration: Declaration = DeclaringClass:'IClassDeclaration';
MethodDeclaration: MemberDeclaration = Symbol:'MethodSymbol' SelfParameter Parameters:NamedParameter*;
AbstractMethodDeclaration: MethodDeclaration = SelfParameter Parameters:NamedParameter*;
ConcreteMethodDeclaration: MethodDeclaration, ConcreteInvocableDeclaration = SelfParameter Parameters:NamedParameter* Body;
ConstructorDeclaration: MemberDeclaration, ConcreteInvocableDeclaration = Symbol:'ConstructorSymbol' ImplicitSelfParameter:SelfParameter Parameters:ConstructorParameter* Body;
FieldDeclaration: MemberDeclaration, ExecutableDeclaration, Binding = Symbol:'FieldSymbol';
AssociatedFunctionDeclaration: MemberDeclaration, ConcreteInvocableDeclaration = Symbol:'FunctionSymbol' Parameters:NamedParameter* Body;

// ---------- Parameters --------------
Parameter = Unused:'bool';
/// A parameter that can be declared in a constructor
ConstructorParameter: Parameter;
/// A parameter that creates a binding, i.e. a named or self parameter
BindingParameter: Parameter, Binding;
NamedParameter: Parameter, ConstructorParameter, BindingParameter, LocalBinding = Symbol:'VariableSymbol' DefaultValue:Expression?;
SelfParameter: Parameter, BindingParameter = Symbol:'SelfParameterSymbol';
FieldParameter: Parameter, ConstructorParameter = ReferencedSymbol:'FieldSymbol' DefaultValue:Expression?;

// ---------- Function Parts
Body: BodyOrBlock = Statements:BodyStatement*;

// ---------- Statements
Statement;
ResultStatement: Statement, BlockOrResult = Expression;
BodyStatement: Statement;
VariableDeclarationStatement: BodyStatement, LocalBinding = NameSpan:'TextSpan' Symbol:'VariableSymbol' Initializer:Expression? VariableIsLiveAfter:'Promise<bool>';
ExpressionStatement: BodyStatement = Expression;

// ---------- Expressions
Expression = DataType:'DataType' Semantics:'ExpressionSemantics';
AssignableExpression: Expression;
BlockExpression: Expression, BlockOrResult, BodyOrBlock = Statements:Statement*;
NewObjectExpression: Expression =  ReferencedSymbol:'ConstructorSymbol' Arguments:Expression*;
UnsafeExpression: Expression = Expression;

// ---------- Literal Expressions
LiteralExpression: Expression;
BoolLiteralExpression: LiteralExpression = Value:'bool';
IntegerLiteralExpression: LiteralExpression = Value:'BigInteger';
NoneLiteralExpression: LiteralExpression;
StringLiteralExpression: LiteralExpression = Value:'string';

// ---------- Operator Expressions
AssignmentExpression: Expression = LeftOperand:AssignableExpression Operator:'AssignmentOperator' RightOperand:Expression;
BinaryOperatorExpression: Expression = LeftOperand:Expression Operator:'BinaryOperator' RightOperand:Expression;
UnaryOperatorExpression: Expression = Fixity:'UnaryOperatorFixity' Operator:'UnaryOperator' Operand:Expression;

// ---------- Control Flow Expressions
IfExpression: Expression, ElseClause = Condition:Expression ThenBlock:BlockOrResult ElseClause?;
LoopExpression: Expression = Block:BlockExpression;
WhileExpression: Expression = Condition:Expression Block:BlockExpression;
ForeachExpression: Expression, LocalBinding = Symbol:'VariableSymbol' InExpression:Expression Block:BlockExpression VariableIsLiveAfterAssignment:'Promise<bool>';
BreakExpression: Expression = Value:Expression?;
NextExpression: Expression;
ReturnExpression: Expression = Value:Expression?;

// ---------- Implicit Conversion Expressions
ImplicitConversionExpression: Expression = Expression DataType:'DataType';
ImplicitImmutabilityConversionExpression: ImplicitConversionExpression = Expression ConvertToType:'ObjectType';
ImplicitNoneConversionExpression: ImplicitConversionExpression = Expression ConvertToType:'OptionalType';
ImplicitNumericConversionExpression: ImplicitConversionExpression = Expression ConvertToType:'NumericType';
ImplicitOptionalConversionExpression: ImplicitConversionExpression = Expression ConvertToType:'OptionalType';

// ---------- Invocation Expressions
InvocationExpression: Expression = Arguments:Expression*;
FunctionInvocationExpression: InvocationExpression = ReferencedSymbol:'FunctionSymbol' Arguments:Expression*;
MethodInvocationExpression: InvocationExpression = Context:Expression ReferencedSymbol:'MethodSymbol' Arguments:Expression*;

// ---------- Variable Expressions
NameExpression: AssignableExpression = ReferencedSymbol:'NamedBindingSymbol' VariableIsLiveAfter:'Promise<bool>';
SelfExpression: Expression = ReferencedSymbol:'SelfParameterSymbol' IsImplicit:'bool';
FieldAccessExpression: AssignableExpression = Context:Expression AccessOperator:'AccessOperator' ReferencedSymbol:'FieldSymbol';
BorrowExpression: Expression = ReferencedSymbol:'BindingSymbol' Referent:Expression;
MoveExpression: Expression = ReferencedSymbol:'BindingSymbol' Referent:Expression;
ShareExpression: Expression = ReferencedSymbol:'BindingSymbol' Referent:Expression;
